windowDef Desktop
{
	comment	"lift gui. (see map matt/lift for implementation example) gui_parm_strogg == 1 sets the lift gui to use non readable strogg text. gui_parm_disable == 1 turns off the button functionality, set it back to == 0 to turn the button back on. namedEvent 'triggerGui' allows you to activate the gui through script as if the player had clicked it."
	rect	0,0,640,480
	backcolor	0,0,0,1
	visible	1
	definevec4	"btn_off"	0.843,0.474,0.141,1
	definevec4	"btn_on"	1,0.325,0,1
	definevec4	"btn_glow"	1,0.854,0.349,1
	float	"pressed"	1

	onInit {
		namedEvent "updateFloor";
		
		if ( "gui::gui_parm_strogg" == 1 ) {
			set "p_strogg::visible" "1";
			set "p_readable::visible" "0";
		} else {
			set "p_strogg::visible" "0";
			set "p_readable::visible" "1";
		}
	}

	onNamedEvent updateFloor {
		if ("gui::floor" == "gui::topfloor" && "desktop::pressed" == "0") { 
			resettime "anim_up_reset" "0" ;
			set "desktop::inmotion" "0" ;
		} else if ("gui::floor" == "gui::bottomfloor" && "desktop::pressed" == "0") { 
			resettime "anim_down_reset" "0" ;
			set "desktop::inmotion" "0" ;
		} 		
	}

	onNamedEvent triggerGui {
		set "button::visible" "0" ;
		
		if ("gui::floor" == "gui::topfloor" ) { 
			set	"cmd" "play guisound_beep2 ; changefloor down" ;
			resettime "anim_down" "0" ;		
			set "desktop::pressed" "1" ;
		} 
		
		if ("gui::floor" == "gui::bottomfloor") { 
			set	"cmd" "play guisound_beep2 ; changefloor up" ;
			resettime "anim_up" "0" ;
			set "desktop::pressed" "1" ;	
		} 
	}

	onNamedEvent triggerDown {
		set "button::visible" "0" ;
		set	"cmd" "play guisound_beep2 ; changefloor down" ;
		resettime "anim_down" "0" ;		
		set "desktop::pressed" "1" ;
	}

	onNamedEvent anim_up {
		resettime "anim_up" "0" ;		
	}

	onNamedEvent anim_up_reset {
		resettime "anim_up_reset" "0" ;		
	}

	onNamedEvent anim_down {
		resettime "anim_down" "0" ;		
	}

	onNamedEvent anim_down_reset {
		resettime "anim_down_reset" "0" ;		
	}

	onNamedEvent disable {
		set "button::visible" "0" ;
	}

	onNamedEvent enable {
		set "button::visible" "1" ;
	}

	windowDef topstuff
	{
		visible	1
		rect	0,0,640,480
		windowDef monitor_darkedge2
		{
			visible	1
			rect	0,0,640,480
			background	"gfx/guis/common/monitor_darkedge2"
			matcolor	0,0,0,1
		}
	}
	windowDef background
	{
		rect	0,0,679,504
		visible	1
		backcolor	0.501,0.501,0.501,1
	}
}
